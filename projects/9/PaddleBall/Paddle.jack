// Define Paddle class
class Paddle {

    // initialize position and size variables
    field int x, y, w, h;

    // Create paddle constructor
    constructor Paddle new(int startX, int startY, int width, int height) {

        let x = startX;
        let y = startY;
        let w = width;
        let h = height;
        return this;

    }

    // Create draw() method
    method void draw() {

        do Screen.setColor(true);
        do Screen.drawRectangle(x, y, x + w - 1, y + h - 1);
        return;

    }

    // Create erase() method
    method void erase() {

        do Screen.setColor(false);
        do Screen.drawRectangle(x, y, x + w - 1, y + h - 1);

        // Set color to true immediately for use in next command
        do Screen.setColor(true);
        return;

    }

    // Create move left logic
    method void moveLeft(int step) {

        do erase();
        let x = x - step;
        if (x < 0) {
            let x = 0;
        }
        do draw();
        return;

    }

    // Create move right logic
    method void moveRight(int step) {

        do erase();
        let x = x + step;
        if ((x + w) > 512) {
            let x = 512 - w;
        }
        do draw();
        return;

    }

    // Define return functions for use in other classes
    method int top() {
        return y;
    }

    method int right() {
        return x + w;
    }

    method int left() {
        return x;
    }

}